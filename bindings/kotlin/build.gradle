buildscript {
    ext {
        kotlin_version = '2.1.20'
    }

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.10.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.diffplug.spotless" version "7.0.3"
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id "maven-publish"
    id "org.jetbrains.dokka" version '2.0.0'
    id "signing"
}

dependencies {
    dokka project(':client')
    dokka project(':server')
}

allprojects {
    repositories {
        // maven {
        //     // Sonatype staging repo. Note this url changes on every publish.
        //     url "https://s01.oss.sonatype.org/service/local/repositories/ieequalitouinet-1287/content/"
        // }
        // mavenLocal()

        google()
        mavenCentral()
    }
}

subprojects {
    layout.buildDirectory.set(rootProject.layout.buildDirectory.dir(name))
}


spotless {
    kotlin {
        target("**/*.kt")
        targetExclude("**/build/**/*.kt")

        ktfmt()
        ktlint()
            .editorConfigOverride(
                ["ktlint_function_naming_ignore_when_annotated_with": "Composable"]
            )

    }
}

dokka {
    pluginsConfiguration.html {
        customStyleSheets.from("${rootDir}/docs/logo-styles.css")
        customAssets.from("${rootDir}/docs/logo-icon.png")
        footerMessage.set("""
            Copyright (c) 2024 <a href="https://equalit.ie">eQualitie</a> |
            <a href="https://ouisync.net">Ouisync website</a> |
            <a href="https://github.com/equalitie/ouisync">Ouisync git repository</a>
            """.stripIndent())
    }

    dokkaPublications.html {
        includes.from(project.layout.projectDirectory.file('README.md'))
    }
}

tasks.named('clean') {
    doLast {
        delete rootProject.layout.buildDirectory.get()
    }
}

// Grab version from the top level Cargo.toml
def cargoManifest = file("$rootDir/../../Cargo.toml").text
def cargoVersionPattern = /(?m)^version\s*=\s*"(.*)"/
def cargoVersion = (cargoManifest =~ cargoVersionPattern)[0][1].trim()

version = "$cargoVersion-$revision"
group   = "ie.equalit.ouinet"

// Use system environment variables for signing and publishing
ext {
    ossrhUsername = System.getenv('OSSRH_USERNAME')
    ossrhPassword = System.getenv('OSSRH_PASSWORD')
    sonatypeStagingProfileId = System.getenv('SONATYPE_STAGING_PROFILE_ID')
    signingKey = System.getenv('SIGNING_KEY')
    signingKeyId = System.getenv('SIGNING_KEY_ID')
    signingPassword = System.getenv('SIGNING_PASSWORD')
}

// Set up Sonatype repository
nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = sonatypeStagingProfileId
            username = ossrhUsername
            password = ossrhPassword
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}

